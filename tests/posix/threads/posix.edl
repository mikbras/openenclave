// Copyright (c) Open Enclave SDK contributors.
// Licensed under the MIT License.

enclave
{
    struct posix_timespec
    {
        int64_t tv_sec;
        uint64_t tv_nsec;
    };

    struct posix_sigaction
    {
        uint64_t handler;
        unsigned long flags;
        uint64_t restorer;
        unsigned mask[2];
    };

    struct posix_siginfo
    {
        uint8_t data[128];
    };

    struct posix_ucontext
    {
        uint8_t data[936];
    };

    struct posix_sigset
    {
        uint64_t data[16];
    };

    trusted
    {
        public void posix_test_ecall(
            [user_check] void* shared_block,
            int tid);

        public int posix_run_thread_ecall(
            uint64_t cookie,
            int tid,
            [user_check] void* shared_block);
    };

    untrusted
    {
        int posix_start_thread_ocall(uint64_t cookie);

        int posix_nanosleep_ocall(
            [in, count=1] const struct posix_timespec* req,
            [out, count=1] struct posix_timespec* rem);

        int posix_gettid_ocall();

        int posix_getpid_ocall();

        int posix_futex_wait_ocall(
            [user_check] int* host_uaddr,
            int val,
            [in, count=1] const struct posix_timespec* timeout);

        int posix_futex_wake_ocall([user_check] int* host_uaddr, int val);

        int posix_futex_requeue_ocall(
            [user_check] int* uaddr,
            int val,
            int val2,
            [user_check] int* uaddr2);

        int posix_clock_gettime_ocall(
            int clk_id,
            [out, count=1] struct posix_timespec* tp);

        long posix_tkill_syscall_ocall(int tid, int sig);

        long posix_rt_sigaction_syscall_ocall(
            int signum,
            [in, count=1] const struct posix_sigaction* act,
            size_t sigsetsize);

        long posix_rt_sigprocmask_syscall_ocall(
            int how,
            [in, count=1] const struct posix_sigset* set,
            [out, count=1] struct posix_sigset* oldset,
            size_t sigsetsize);

        void posix_noop_ocall();

        ssize_t posix_write_ocall(
            int fd,
            [in, size=size] const void* data,
            size_t size);

        void posix_assume_ocall(
            [in, string] const char* file,
            uint32_t line,
            [in, string] const char* func,
            [in, string] const char* cond,
            [in, count=backtrace_count] const uint64_t* backtrace,
            size_t backtrace_count);

        void posix_panic_ocall(
            [in, string] const char* file,
            uint32_t line,
            [in, string] const char* func,
            [in, string] const char* msg,
            [in, count=backtrace_count] const uint64_t* backtrace,
            size_t backtrace_count);

        int posix_thread_wait_ocall(
            [user_check] int* uaddr,
            [in, count=1] struct posix_timespec* timeout);

        int posix_thread_wake_ocall(
            [user_check] int* uaddr);

        int posix_thread_wake_wait_ocall(
            [user_check] int* waiter_uaddr,
            [user_check] int* self_uaddr,
            [in, count=1] struct posix_timespec* timeout);
    };
};
